/**
 * @desc Templates
 */
module S1AP_Templates {

    import from S1AP_Constants language "ASN.1:1997" all;
	import from S1AP_CommonDataTypes language "ASN.1:1997" all;
//    import from S1AP_Containers language "ASN.1:1997" all;
    import from S1AP_IEs language "ASN.1:1997" all;
    import from S1AP_PDU_Contents language "ASN.1:1997" all;
    import from S1AP_PDU_Descriptions language "ASN.1:1997" all;

	import from S1AP_Types all;

	group g_initiatingMessage {    	
		
	template S1AP_PDU m_initiatingMessage (ProcedureCode p_procedureCode, anytype p_value):= {
		initiatingMessage := {
			procedureCode := p_procedureCode,
			criticality := reject,
			value_ := p_value
		}
	}

	template S1AP_PDU m_initiatingMessage_E_RABSetup(RecordOf_E_RABSetupRequestIEs p_value) := {
		initiatingMessage := {
			procedureCode := id_E_RABSetup,
			criticality := reject,
			value_ := {RecordOf_E_RABSetupRequestIEs := p_value}
		}
	}
	
    } // end of group g_initiatingMessage

	group g_outcomeMessage {

		template S1AP_PDU mw_successfulOutcome(template ProcedureCode
												   p_procedureCode := ?,
											   template anytype p_value) := {
			successfulOutcome := {
				procedureCode := p_procedureCode,
				criticality := reject,
				value_ := {
					RecordOf_E_RABSetupResponseIEs := {
						mw_E_RABSetupResponseIEs_MME,
						mw_E_RABSetupResponseIEs_ENB,
						mw_E_RABSetupResponseIEs_ERAB
					}
				}
			}
		}

		template S1AP_PDU mw_successfulOutcome_E_RABSetupResponse
		(template ProcedureCode p_procedureCode := ?,
											   template RecordOf_E_RABSetupResponseIEs p_value) := {
			successfulOutcome := {
				procedureCode := p_procedureCode,
				criticality := reject,
				value_ := {
					RecordOf_E_RABSetupResponseIEs := p_value
				}
			}
		}

	} // end of group g_outcomeMessage
	
group g_E_RAB_SetupRequest {

	template(value) E_RABSetupRequest.protocolIEs[-] m_E_RABSetupRequestIEs_MME (MME_UE_S1AP_ID p_value:= 1):= {
		id := S1AP_Constants.id_MME_UE_S1AP_ID,
		criticality := S1AP_CommonDataTypes.reject,
		value_ := { MME_UE_S1AP_ID := p_value }
	}
	template(value) E_RABSetupRequest.protocolIEs[-] m_E_RABSetupRequestIEs_ENB (ENB_UE_S1AP_ID p_value:= 1):= {
		id := S1AP_Constants.id_eNB_UE_S1AP_ID,
		criticality := S1AP_CommonDataTypes.reject,
		value_ := { ENB_UE_S1AP_ID := p_value }
	}		
	template(value) E_RABSetupRequest.protocolIEs[-] m_E_RABSetupRequestIEs_ERAB (E_RABToBeSetupListBearerSUReq p_value := m_e_rABToBeSetupListBearerSUReq({m_E_RABToBeSetupItemBearerSUReq})):= {
		id := S1AP_Constants.id_E_RABToBeSetupListBearerSUReq,
		criticality := S1AP_CommonDataTypes.reject,
		value_ := { E_RABToBeSetupListBearerSUReq := p_value }
	}

	template(value) E_RABToBeSetupListBearerSUReq m_e_rABToBeSetupListBearerSUReq (template RecordOf_E_RABToBeSetupItemBearerSUReq p_value := {m_E_RABToBeSetupItemBearerSUReq}):= {
		{
			id := id_E_RABToBeSetupItemBearerSUReq, 
			criticality := reject,
			value_ := { RecordOf_E_RABToBeSetupItemBearerSUReq := valueof(p_value) }
		}
	}

	template(value) E_RABToBeSetupItemBearerSUReq m_E_RABToBeSetupItemBearerSUReq:=  {
			e_RAB_ID                        := 0,
			e_RABlevelQoSParameters         := {
				qCI := 0,
				allocationRetentionPriority := {
					priorityLevel               := 0,
					pre_emptionCapability       := shall_not_trigger_pre_emption,
					pre_emptionVulnerability    := pre_emptable,
					iE_Extensions               :=omit
				},
				gbrQosInformation := omit,
				iE_Extensions := omit
			},
			transportLayerAddress           := '0'B,
			gTP_TEID                        := '00000000'O,
			nAS_PDU                         := '0000'O,
			iE_Extensions                   := omit
		}			
} // end of group g_E_RAB_SetupRequest

group g_E_RAB_SetupResponse {
	
	template E_RABSetupResponse.protocolIEs[-] mw_E_RABSetupResponseIEs_MME := {
		id := S1AP_Constants.id_MME_UE_S1AP_ID,
		criticality := S1AP_CommonDataTypes.ignore,
		value_ := { MME_UE_S1AP_ID := ? }
	}

	template E_RABSetupResponse.protocolIEs[-] mw_E_RABSetupResponseIEs_ENB := {
		id := S1AP_Constants.id_eNB_UE_S1AP_ID,
		criticality := S1AP_CommonDataTypes.ignore,
		value_ := { ENB_UE_S1AP_ID := ? }
	}

	template E_RABSetupResponse.protocolIEs[-] mw_E_RABSetupResponseIEs_ERAB := {
		id := S1AP_Constants.id_E_RABSetupListBearerSURes,
		criticality := S1AP_CommonDataTypes.ignore,
		value_ := { E_RABSetupItemBearerSURes := mw_E_RABSetupItemBearerSURes }
	}
	
	template(present) E_RABSetupItemBearerSURes mw_E_RABSetupItemBearerSURes:=  {
		e_RAB_ID                        := ?,
		transportLayerAddress           := '?'B,
		gTP_TEID                        := ?,
		iE_Extensions                   := *
	}
	
} // end of group g_E_RAB_SetupResponse

}